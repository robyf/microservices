plugins {
    id 'java'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.springframework.boot' version '2.3.2.RELEASE'
    id 'io.franzbecker.gradle-lombok' version '4.0.0'
    id 'org.sonarqube' version '3.0'
    id 'maven-publish'
}

repositories {
    jcenter()
    maven {
        url = "/Users/roberto/work/microservices/repo"
    }
}

dependencyManagement {
    imports {
        mavenBom 'net.robyf.ms:microservices-dependencies:Akaa.SR1-SNAPSHOT'
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, "minutes"
}

group = "net.robyf.ms"
version = "local"

dependencies {
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-sleuth'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-zipkin'
    implementation group: 'net.robyf.ms', name: 'microservices-spring-boot-starter'
    implementation group: 'com.h2database', name: 'h2'
    implementation group: 'org.zalando', name: 'problem-spring-web'
    implementation group: 'io.springfox', name: 'springfox-boot-starter'
    implementation group: 'io.springfox', name: 'springfox-swagger-ui'
    implementation group: 'javax.ws.rs', name: 'javax.ws.rs-api'
    implementation group: 'com.auth0', name: 'java-jwt'

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testImplementation group: 'junit', name: 'junit'
}

processResources {
    expand(project.properties)
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

test.finalizedBy(project.tasks.jacocoTestReport)

bootRun {
    systemProperties = [
            'spring.profiles.active': 'local'
    ]
}

task apiJar(type: Jar) {
    archiveBaseName = 'scoring-service-api'
    from sourceSets.main.output
    include '**/api/**'
}

artifacts {
    archives apiJar
}

publishing {
    repositories {
        maven {
            url = "/Users/roberto/work/microservices/repo"
        }
    }

    publications {
        maven(MavenPublication) {
            artifactId = 'scoring-service-api'
            version = '1.0-SNAPSHOT'
            artifact apiJar
        }
    }
}
