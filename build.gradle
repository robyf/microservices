plugins {
    id 'java'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.springframework.boot' version '2.3.2.RELEASE'
    id 'io.franzbecker.gradle-lombok' version '4.0.0'
    id 'org.sonarqube' version '3.0'
    id 'maven-publish'
}

repositories {
    jcenter()
    maven {
        url = "/Users/roberto/work/microservices/repo"
    }
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR7'
    }
}

group = "net.robyf.ms"
version = "local"

dependencies {
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-sleuth'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-zipkin'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
    implementation group: 'com.h2database', name: 'h2'
    implementation group: 'org.zalando', name: 'problem-spring-web', version: '0.25.2'
    implementation group: 'io.springfox', name: 'springfox-boot-starter', version: '3.0.0'
    implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: '3.0.0'
    implementation group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1.1'
    implementation group: 'com.auth0', name: 'java-jwt', version: '3.10.3'

    implementation group: 'net.robyf.ms', name: 'scoring-service-api', version: '1.0-SNAPSHOT'

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testImplementation group: 'junit', name: 'junit'
}

processResources {
    expand(project.properties)
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

test.finalizedBy(project.tasks.jacocoTestReport)

bootRun {
    systemProperties = [
            'spring.profiles.active': 'local'
    ]
}

task apiJar(type: Jar) {
    archiveBaseName = 'lending-service-api'
    from sourceSets.main.output
    include '**/api/**'
}

artifacts {
    archives apiJar
}

publishing {
    repositories {
        maven {
            url = "/Users/roberto/work/microservices/repo"
        }
    }

    publications {
        maven(MavenPublication) {
            artifactId = 'lending-service-api'
            version = '1.0-SNAPSHOT'
            artifact apiJar
        }
    }
}
